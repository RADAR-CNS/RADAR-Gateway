buildscript {
    ext.kotlinVersion = '1.2.10'
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id 'war'
    id "org.jetbrains.kotlin.jvm" version '1.2.10'
}

group 'org.radarcns'
version '0.2-SNAPSHOT'

ext.githubRepoName = 'RADAR-CNS/RADAR-Gateway'
ext.githubUrl = 'https://github.com/' + githubRepoName + '.git'
ext.issueUrl = 'https://github.com/' + githubRepoName + '/issues'
ext.website = 'http://radar-cns.org'
ext.description = 'RADAR Gateway to handle secured data flow to backend.'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.okhttp3Version = '3.9.0'
ext.radarMpVersion = '0.3.1'
ext.httpProxyVersion = '1.9'
ext.apacheHttpVersion = '4.5.2'
ext.javaxServletVersion = '3.1.0'
ext.jacksonVersion = '2.9.3'
ext.slf4jVersion = '1.7.25'
ext.logbackVersion = '1.2.3'
ext.tomcatVersion = '8.5.24'

ext.junitVersion = '5.0.1'
ext.mockitoVersion = '2.+'

repositories {
    jcenter()
    // Non-jcenter radar releases
    maven { url 'https://dl.bintray.com/radar-cns/org.radarcns' }
    // For working with dev-branches
    maven { url 'https://oss.jfrog.org/artifactory/libs-snapshot/' }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlinVersion

    compile (group: 'org.radarcns', name: 'radar-auth', version: radarMpVersion) {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp3Version

    runtimeOnly group: 'org.mitre.dsmiley.httpproxy' , name: 'smiley-http-proxy-servlet' , version: httpProxyVersion
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

    providedCompile group: 'org.apache.tomcat' , name: 'tomcat-catalina' , version: tomcatVersion
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

//---------------------------------------------------------------------------//
// Build system metadata                                                     //
//---------------------------------------------------------------------------//

ext.sharedManifest = manifest {
    attributes("Implementation-Title": rootProject.name,
            "Implementation-Version": version)
}

war {
    archiveName = "${rootProject.name}.war"
    manifest.from sharedManifest
}

task wrapper(type: Wrapper) {
    gradleVersion '4.4'
}

apply from: 'gradle/publishing.gradle'
apply from: 'gradle/test.gradle'
apply from: 'gradle/util.gradle'
