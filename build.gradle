plugins {
    id 'application'
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id 'com.avast.gradle.docker-compose' version "0.12.1" apply false
}

group 'org.radarbase'
version '0.5.1'

ext {
    githubRepoName = 'RADAR-Base/RADAR-Gateway'
    githubUrl = 'https://github.com/' + githubRepoName + '.git'
    issueUrl = 'https://github.com/' + githubRepoName + '/issues'
    website = 'http://radar-base.org'
    description = 'RADAR Gateway to handle secured data flow to backend.'

    okhttp3Version = '4.7.2'
    radarAuthVersion = '0.2.3'
    radarCommonsVersion = '0.13.0'
    radarSchemasVersion = '0.5.10.1'
    jacksonVersion = '2.11.0'
    slf4jVersion = '1.7.30'
    logbackVersion = '1.2.3'
    grizzlyVersion = '2.4.4'
    lzfseVersion = '0.1.0'
    kafkaVersion = "2.5.0"
    confluentVersion = "5.5.0"

    junitVersion = '5.6.2'
}

repositories {
    jcenter()
    // Non-jcenter radar releases
    maven { url 'https://dl.bintray.com/radar-cns/org.radarcns' }
    maven { url 'https://dl.bintray.com/radar-base/org.radarbase' }
    // For working with dev-branches
    maven { url 'https://repo.thehyve.nl/content/repositories/snapshots' }
    maven { url 'https://oss.jfrog.org/artifactory/libs-snapshot/' }
    maven { url "https://packages.confluent.io/maven/" }
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'

    implementation group: 'org.radarbase', name: 'radar-commons', version: radarCommonsVersion
    implementation group: 'org.radarbase', name: 'radar-jersey', version: radarAuthVersion
    implementation group: 'org.radarbase', name: 'lzfse-decode', version: lzfseVersion

    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

    implementation "org.apache.kafka:kafka-clients:$kafkaVersion"
    implementation "io.confluent:kafka-avro-serializer:$confluentVersion"

    // JMX monitoring
    runtimeOnly group: 'org.glassfish.grizzly', name: 'grizzly-framework-monitoring', version: grizzlyVersion
    runtimeOnly group: 'org.glassfish.grizzly', name: 'grizzly-http-monitoring', version: grizzlyVersion
    runtimeOnly group: 'org.glassfish.grizzly', name: 'grizzly-http-server-monitoring', version: grizzlyVersion

    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

application {
    mainClassName = 'org.radarbase.gateway.MainKt'

    applicationDefaultJvmArgs = [
        "-Dcom.sun.management.jmxremote",
        "-Dcom.sun.management.jmxremote.local.only=false",
        "-Dcom.sun.management.jmxremote.port=9010",
        "-Dcom.sun.management.jmxremote.authenticate=false",
        "-Dcom.sun.management.jmxremote.ssl=false"
    ]
}

if (!hasProperty('profile')) {
    ext.profile = 'dev'
}

apply from: "gradle/profile.${profile}.gradle"

wrapper {
    gradleVersion '6.4.1'
}
