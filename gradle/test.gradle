apply plugin: 'docker-compose'

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir 'src/integrationTest/kotlin'
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    maven { url 'https://packages.confluent.io/maven/' }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: okhttp3Version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

    testImplementation group: 'org.radarcns', name: 'radar-schemas-commons', version: radarSchemasVersion

    integrationTestImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp3Version
    integrationTestImplementation group: 'org.radarcns', name: 'radar-schemas-commons', version: radarSchemasVersion
    integrationTestImplementation group: 'org.radarbase', name: 'radar-commons-testing', version: radarCommonsVersion
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

compileIntegrationTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = "Run integration tests (located in src/integrationTest/...)."
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    outputs.upToDateWhen { false }
    testLogging.events "skipped", "failed", "passed"
}

//---------------------------------------------------------------------------//
// Testing                                                                   //
//---------------------------------------------------------------------------//

tasks.matching { it instanceof Test }.all {
    useJUnitPlatform ()

    testLogging {
        showStandardStreams = true
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat "full"
    }
    shouldRunAfter test
}

dockerCompose.isRequiredBy(integrationTest)

dockerCompose {
    useComposeFiles = ['src/integrationTest/docker/docker-compose.yml']
    stopContainers = project.hasProperty("dockerCompose.stopContainers") ? Boolean.parseBoolean(project.property("dockerCompose.stopContainers") as String) : true
    waitForTcpPortsTimeout = Duration.ofMinutes(3)
    captureContainersOutput = true
}
